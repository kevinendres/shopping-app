plugins {
    id 'java'
    id "io.freefair.lombok" version "5.1.0"
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'info.solidsoft.pitest' version '1.6.0'
    id 'pmd'
    id 'jacoco'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.3.0.RELEASE'
    implementation 'javax.persistence:javax.persistence-api:2.2'
    implementation 'org.projectlombok:lombok:1.18.12'
    implementation 'org.apache.commons:commons-math3:3.5'
    implementation 'com.h2database:h2:1.4.200'
    implementation 'org.jdbi:jdbi3-core:3.14.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.3.0.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
}

group = 'edu.depaul.se433'
version = '0.0.1-SNAPSHOT'
description = 'shopping-app'

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

def user = 'kendres'

bootJar {
    mainClassName = 'edu.depaul.se433.shoppingapp.ShoppingAppApplication'
}

test {
    ignoreFailures(true)
    useJUnitPlatform ()
    finalizedBy jacocoTestReport
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
    jacoco {
        excludes = [
                "edu.depaul.se433.shoppingapp.Accounts",
                "edu.depaul.se433.shoppingapp.Purchase",
                "edu.depaul.se433.shoppingapp.PurchaseItem",
                "edu.depaul.se433.shoppingapp.ShippingType",
                "edu.depaul.se433.shoppingapp.ShoppingAppApplication",
                "edu.depaul.se433.shoppingapp.ShoppingCartApi"
        ]
    }

}

task packageDistribution(type: Zip) {
    dependsOn test
    archiveFileName = "${user}-${rootProject.name}.zip"
    destinationDirectory = file("$buildDir/dist")

    from("$projectDir") {
        include "src/**/*"
        include "build/reports/pitest/**/*"
        include "build/reports/pmd/**/*"
        include "build/reports/jacoco/**/*"
        include "build.gradle"
        include "gradlew"
        include "gradlew.bat"
        include "settings.gradle"
    }

    into("${user}-${rootProject.name}")
}

build {
    dependsOn 'pitest'
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}

pitest {
    junit5PluginVersion = '0.12'
    targetClasses = ['edu.depaul.se433.shoppingapp.PurchaseAgent', 'edu.depaul.se433.shoppingapp.TotalCostCalculator']
    targetTests = ['edu.depaul.se433.shoppingapp.TestPurchaseAgent', 'edu.depaul.se433.shoppingapp.TestTotalCostCalculator']
    timestampedReports = false
    skipFailingTests = true
}

pmd {
    consoleOutput = true
    toolVersion = "6.21.0"
    rulePriority = 5
    ignoreFailures = true
    ruleSets = [
            "category/java/errorprone.xml",
            "category/java/bestpractices.xml",
            "category/java/performance.xml",
            "category/java/design.xml"
    ]
}

check {
    finalizedBy packageDistribution
}


